apply plugin: 'com.android.library'
apply plugin: 'digital.wup.android-maven-publish'

android {
    compileSdkVersion 28


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        packagingOptions {
            exclude "META-INF/LICENSE*"
        }
    }
}

dependencies {
    // Virgil
    api(project(":crypto")) {
        exclude group: 'com.virgilsecurity.crypto', module: 'common'
        exclude group: 'com.virgilsecurity.crypto', module: 'foundation'
    }
    api "com.virgilsecurity.crypto:foundation-android:$versions.virgilCrypto"

    // Tests
    testImplementation "junit:junit:$versions.junitAndroid"
    androidTestImplementation "com.android.support.test:runner:$versions.testsRunner"
    androidTestImplementation "org.apache.commons:commons-lang3:$versions.commonsLang"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.android
            artifact androidJavadocsJar
            artifact androidSourcesJar

            pom {
                name = 'Virgil Crypto for Android'
            }
            artifactId = "crypto-android"
        }
    }
}
