apply plugin: 'kotlin'

dependencies {
    // Internal dependencies
    api project(":common")
    implementation project(":crypto")
    api project(":api")

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlinVersion"

    // Gson
    implementation "com.google.code.gson:gson:$versions.gson"

    // Tests
    testImplementation "org.junit.jupiter:junit-jupiter:$versions.junit"
    testImplementation "org.apache.commons:commons-lang3:$versions.commonsLang"
    testImplementation "org.mockito:mockito-core:$versions.mockito"
    testImplementation "org.mockito:mockito-junit-jupiter:$versions.mockitoJunit"

    testImplementation project(":test-common")

    testImplementation "com.virgilsecurity.crypto:foundation:$versions.virgilCrypto"
    testImplementation "com.virgilsecurity.crypto:ratchet:$versions.virgilCrypto"
}

sourceSets.main.java.srcDirs = ["${buildDir}/generated/", "src/main/java"]

task generateVirgilInfo(type: Copy) {
    from('src/main/templates') {
        include '**/*.java'
    }
    into "$buildDir/generated"
    expand(version: "$project.version")
    includeEmptyDirs = false
}

compileKotlin.dependsOn(generateVirgilInfo)
compileJava.dependsOn(generateVirgilInfo)

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.kotlin
            pom {
                name = 'Virgil SDK'
            }
            artifactId = "sdk"
        }
    }
}
